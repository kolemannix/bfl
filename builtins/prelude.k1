type unit = builtin
type char = builtin
type bool = builtin
type string = builtin
type never = builtin
type Pointer = builtin

type alias int = i64
type alias uint = u64
type alias byte = u8

intern fn sizeOf[T](): u64
intern fn alignOf[T](): u64
intern fn typeId[T](): bool
fn typeEq[T, U](): bool {
  typeId[T]() == typeId[U]()
}

extern fn _k1_readFileToString(path: string): string
extern fn _k1_crash(reason: string*, file: string*, line: u64): never

extern fn _k1_malloc(size: u64): Pointer
extern fn _k1_free(ptr: Pointer): unit

intern fn referenceSet[T](t: T*, value: T): unit

namespace Pointer {
  // This is an intrinsic so that we can codegen a getelementptr
  // instruction in LLVM rather than a pointer cast.
  // But we _could_ do it in userland using sizeOf and arithmetic which is neat!
  intern fn refAtIndex[T](self: Pointer, index: u64): T*

  // TODO: memset intrinsic
  // intern fn memset(self: Pointer, num: u64, value: u8): unit
}

type Slice[T] = { len: u64, data: Pointer, sadPhantomT: T? }
namespace Slice {
    fn get[T](self: Slice[T], index: u64): T {
        // TODO: Annotate as cold for llvm
        if index >= self.len {
          crash("Slice index out of bounds")
        };
        val resultT: T* = self.data.refAtIndex(index);
        *resultT
    }

    fn set[T](self: Slice[T], index: u64, value: T): unit {
        // TODO: Annotate as cold for llvm
        if index >= self.len {
          crash("Slice index out of bounds")
        };
        val resultT: T* = self.data.refAtIndex(index);
        referenceSet(resultT, value)
    }
}

//type alias opaque string = { len: u64, data: Pointer }

// Must have ability id 0
ability Equals {
  fn equals(self: Self, other: Self): bool
}

intern fn printInt(value: i64): unit
intern fn print(value: string): unit
intern fn exit(code: i64): unit

fn crash(msg: string): never {
  _k1_crash(&msg, &compilerFile(), compilerLine());
}

namespace Files {
  fn readToString(path: string): string {
    _k1_readFileToString(path)
  }
}

fn printBool(value: bool): unit {
  if value { print("true") } else { print("false") }
}

fn assert(value: bool): unit {
  if not value {
    crash("ASSERT FAILED")
  };
}
fn assertMsg(msg: string, value: bool): unit {
  if not value {
    crash("ASSERT FAILED: ".concat(msg))
  };
}
namespace char {
  fn toString(self: char): string {
    string::fromChars([self]);
  }
}

type NewArray[T] = { len: u64, cap: u64, data: Pointer, phantom: T? }
namespace NewArray {
  fn new[T](len: u64): NewArray[T] {
    val sizeBytes = sizeOf[T]();
    val ptr = _k1_malloc(sizeBytes * len);
    // No need to zero out since we'll be using our own allocator eventually which will zero on mmap()
    { len: len, cap: len, data: ptr, phantom: None }
  }
  fn getRef[T](self: NewArray[T], index: u64): T* {
    // This line actually exercises type inference pretty well!
    val tRef: T* = self.data.refAtIndex(index);
    tRef
  }
  fn get[T](self: NewArray[T], index: u64): T {
    val tRef = self.getRef(index);
    *tRef
  }
  fn set[T](self: NewArray[T], index: u64, value: T): unit {
    val tRef = self.getRef(index);
    referenceSet(tRef, value)
  }
}

namespace Array {

  intern fn get[T](self: Array[T], index: u64): T
  intern fn set[T](self: Array[T], index: u64, value: T): unit

  intern fn new[T](len: u64): Array[T]
  intern fn length[T](self: Array[T]): u64
  intern fn capacity[T](self: Array[T]): u64
  intern fn grow[T](self: Array[T]): unit
  intern fn set_length[T](self: Array[T], new_length: u64): unit


  fn push[T](self: Array[T], elem: T): unit {
    val start_length = self.length();
    if start_length == self.capacity() {
      self.grow();
    };
    self.set(start_length, elem);
    self.set_length(start_length + 1);
  }

  fn first[T](self: Array[T]): T? {
    if self.length() > 0 {
      self.get(0)
    } else None
  }

  fn last[T](self: Array[T]): T? {
    if self.length() > 0 {
      self.get(self.length() - 1)
    } else None
  }
}

impl Equals for Array[int] {
  // todo: Obviously this should be a memcmp
  fn equals(self: Array[int], other: Array[int]): bool {
    if self.length() != other.length() {
      false
    } else {
      mut i: u64 = 0;
      while i < self.length() {
        if self.get(i) != other.get(i) {
          return(false);
        };
        i = i + 1;
      };
      true
    }
  }
}

impl Equals for string {
  intern fn equals(self: string, that: string): bool
}

impl Equals for Array[string] {
  fn equals(self: Array[string], other: Array[string]): bool {
    if self.length() != other.length() {
      false
    } else {
      mut i: u64 = 0;
      while i < self.length() {
        if self.get(i) != other.get(i) {
          return(false);
        };
        i = i + 1;
      };
      true
    }
  }
}

namespace string {

  intern fn get(self: string, index: u64): char
  intern fn set(self: string, index: u64, value: char): unit

  intern fn fromChars(bytes: Array[char]): string
  intern fn length(self: string): u64
  fn index_of(self: string, c: char): i64 {
    mut i: u64 = 0;
    while i < self.length() {
      if (self.get(i) == c) {
        return(i as i64);
      };
      i = i + 1;
    };
    -1
  }
  fn concat(self: string, other: string): string {
    val new_length = self.length() + other.length();
    val copied = Array::new[char](new_length);
    mut i: u64 = 0;
    while (i < self.length()) {
      copied.set(i, self.get(i));
      i = i + 1;
    };
    i = 0;
    while (i < other.length()) {
      copied.set(i + self.length(), other.get(i));
      i = i + 1;
    };
    fromChars(copied)
  }
}

fn println(value: string): unit {
  print(value.concat('\n'.toString()));
}
