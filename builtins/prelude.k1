type unit = builtin
type char = builtin
type bool = builtin
type string = builtin
type never = builtin

intern fn sizeOf<T>(): int
intern fn alignOf<T>(): int
intern fn typeId<T>(): bool
fn typeEq<T, U>(): bool {
  typeId<T>() == typeId<U>()
}

extern fn _k1_charToString(c: char): string
extern fn _k1_intToChar(i: int): char
extern fn _k1_readFileToString(path: string): string
extern fn _k1_crash(reason: string*, file: string*, line: u64): never

type alias int = i64
type alias uint = u64
type alias byte = u8

// extern fn _k1_malloc(size: int): RawPointer
// extern fn _k1_free(ptr: RawPointer): unit

//type alias opaque string = { len: int, data: RawPointer }

// Must have type id 6; do not reorder!
type alias opaque RawPointer = u64
namespace RawPointer {
  fn toIntUnsafe(self: RawPointer): u64 { self }
  fn fromIntUnsafe(value: u64): RawPointer { value }
}

// Must have ability id 0
ability Equals {
  fn equals(self: Self, other: Self): bool
}

intern fn printInt(value: i64): unit
intern fn print(value: string): unit
intern fn exit(code: i64): unit

fn crash(msg: string): never {
  _k1_crash(&msg, &compilerFile(), compilerLine());
}

namespace Files {
  fn readToString(path: string): string {
    _k1_readFileToString(path)
  }
}

fn printBool(value: bool): unit {
  if value { print("true") } else { print("false") }
}

fn assert(value: bool): unit {
  if not value {
    crash("ASSERT FAILED")
  };
}
fn assertMsg(msg: string, value: bool): unit {
  if not value {
    crash("ASSERT FAILED: ".concat(msg))
  };
}
namespace char {
  fn fromInt(value: int): char {
    _k1_intToChar(value)
  }
  fn toString(self: char): string {
    _k1_charToString(self)
  }
}

namespace Array {
  intern fn new<T>(len: u64): Array<T>
  intern fn length<T>(self: Array<T>): u64
  intern fn capacity<T>(self: Array<T>): u64
  intern fn grow<T>(self: Array<T>): unit
  intern fn set_length<T>(self: Array<T>, new_length: u64): unit
  fn push<T>(self: Array<T>, elem: T): unit {
    val start_length = self.length<T>();
    if start_length == self.capacity<T>() {
      self.grow<T>();
    };
    self[start_length] = elem;
    self.set_length<T>(start_length + 1);
  }


  fn first<T>(self: Array<T>): T? {
    if self.length() > 0 {
      self[0]
    } else None
  }

  fn last<T>(self: Array<T>): T? {
    if self.length() > 0 {
      self[self.length() - 1]
    } else None
  }
}

impl Equals for Array<int> {
  // todo: Obviously this should be a memcmp
  fn equals(self: Array<int>, other: Array<int>): bool {
    if self.length() != other.length() {
      false
    } else {
      mut i: u64 = 0;
      while i < self.length() {
        if self[i] != other[i] {
          return(false);
        };
        i = i + 1;
      };
      true
    }
  }
}

impl Equals for string {
  intern fn equals(self: string, that: string): bool
}

impl Equals for Array<string> {
  fn equals(self: Array<string>, other: Array<string>): bool {
    if self.length() != other.length() {
      false
    } else {
      mut i: u64 = 0;
      while i < self.length() {
        if self[i] != other[i] {
          return(false);
        };
        i = i + 1;
      };
      true
    }
  }
}

namespace string {
  intern fn fromChars(bytes: Array<char>): string
  intern fn length(self: string): u64
  fn index_of(self: string, c: char): i64 {
    mut i: u64 = 0;
    while i < self.length() {
      if (self[i] == c) {
        return(i as i64);
      };
      i = i + 1;
    };
    -1
  }
  fn concat(self: string, other: string): string {
    val new_length = self.length() + other.length();
    val copied = Array::new<char>(new_length);
    mut i: u64 = 0;
    while (i < self.length()) {
      copied[i] = self[i];
      i = i + 1;
    };
    i = 0;
    while (i < other.length()) {
      copied[i + self.length()] = other[i];
      i = i + 1;
    };
    fromChars(copied)
  }
}

fn println(value: string): unit {
  print(value.concat('\n'.toString()));
}
